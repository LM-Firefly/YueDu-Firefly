plugins {
//    id "com.android.application"
//    id 'org.jetbrains.kotlin.android'
//    id 'kotlin-parcelize'
//    //id 'kotlin-kapt'
//    id 'com.google.devtools.ksp'
//    id "com.google.gms.google-services"

    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.parcelize
    alias libs.plugins.room
    alias libs.plugins.ksp
    // alias libs.plugins.google.services
    // google-services plugin removed: it required google-services.json and Play services integration
}
apply from: 'download.gradle'

static def releaseTime() {
    return new Date().format("yy.MMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

def name = "YueDu"
def version = "3." + releaseTime()
def gitCommits = 0
try {
    def proc = "git rev-list HEAD --count".execute(null, rootProject.rootDir)
    proc.waitFor()
    if (proc.exitValue() == 0) {
        gitCommits = Integer.parseInt(proc.in.text.trim())
    } else {
        def digits = (proc.err?.text ?: "").find(/\d+/) ?: "0"
        gitCommits = Integer.parseInt(digits)
    }
} catch (Exception ignored) {
    gitCommits = Integer.parseInt(System.getenv("GIT_COMMIT_COUNT") ?: "0")
}

// 根据 version 字符串生成 versionCode：
// 规则：取 version 中的数字（去掉中间的 "."），尝试转换为整数；
// 由于 Android 要求 versionCode 为 32 位有符号整数，若数字过大则取数字的后 9 位作为 versionCode 以避免溢出；
// 若解析失败则回退到之前的基于 gitCommit 的方案（10000 + gitCommits）。
def versionCodeFromVersion(String ver, int fallback) {
    try {
    def digits = ver.replaceAll("[^0-9]", "")
        if (!digits) return fallback
        // 若长度过长，取最后 9 位保证小于 Integer.MAX_VALUE
        if (digits.length() > 9) {
            digits = digits.substring(digits.length() - 9)
        }
        return Integer.parseInt(digits)
    } catch (Exception e) {
        return fallback
    }
}

android {
    compileSdk = compile_sdk_version
    namespace = 'io.legado.app'
    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(24))
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '24'
        }
    }

    signingConfigs {
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            myConfig {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
                v1SigningEnabled = true
                v2SigningEnabled = true
                enableV3Signing = true
                enableV4Signing = true
            }
        }
    }
    defaultConfig {
    applicationId = "io.legado.app"
    minSdk = 23
    targetSdk = 36
    versionCode = versionCodeFromVersion(version, 10000 + gitCommits)
    versionName = version
    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", name + "_" + version)

        buildConfigField "String", "Cronet_Version", "\"$CronetVersion\""
        buildConfigField "String", "Cronet_Main_Version", "\"$CronetMainVersion\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental"     : "true",
                        "room.expandProjection": "true",
                        "room.schemaLocation"  : "$projectDir/schemas".toString()
                ]
            }
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
    }
    buildTypes {
        release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.myConfig
            }
            applicationIdSuffix = '.Firefly'
//            if (getApplicationIdSuffix() == '.Firefly') {
//               manifestPlaceholders.put("app_name", "@string/app_name_a")
//            }else {
                manifestPlaceholders.put("app_name", "@string/app_name")
//            }

            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'cronet-proguard-rules.pro'
        }
        debug {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.myConfig
            }
            manifestPlaceholders.put("app_name", "@string/app_name")

            applicationIdSuffix = '.debug'
            versionNameSuffix = 'debug'
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'cronet-proguard-rules.pro'
        }
    }
    //noinspection GrDeprecatedAPIUsage
    flavorDimensions = ['mode']
    productFlavors {
        app {
            dimension = "mode"
            manifestPlaceholders.put("APP_CHANNEL_VALUE", "app")
        }
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def flavor = variant.productFlavors[0].name
            outputFileName = "${name}_${flavor}_${defaultConfig.versionName}.apk"
        }
    }


    room {
        schemaDirectory "$projectDir/schemas"
    }
    // 设定Room的KSP参数
    ksp {
        arg("room.incremental", "true")
        arg("room.expandProjection", "true")
        arg("room.generateKotlin", "false")
        //arg("room.schemaLocation", "$projectDir/schemas")

    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled = true
        // Sets Java compatibility
        sourceCompatibility = JavaVersion.VERSION_24
        targetCompatibility = JavaVersion.VERSION_24
    }
    packaging {
        resources.excludes.add('META-INF/*')
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    lint {
        checkDependencies = true
    }
    tasks.withType(JavaCompile).tap {
        configureEach {
            //options.compilerArgs << "-Xlint:unchecked"
        }
    }
}

dependencies {
    //compileOnly "com.android.tools.build:gradle:$agp_version"
    //noinspection GradleDependency,GradlePackageUpdate
    //coreLibraryDesugaring('com.android.tools:desugar_jdk_libs_nio:2.0.4')
    coreLibraryDesugaring(libs.desugar)
    testImplementation(libs.junit)
    androidTestImplementation(libs.bundles.androidTest)
    //kotlin
    //noinspection GradleDependency,DifferentStdlibGradleVersion
    implementation(libs.kotlin.stdlib)
    //Kotlin反射
    //noinspection GradleDependency,DifferentStdlibGradleVersion
    //implementation(libs.kotlin.reflect)


    //协程
    //def coroutines_version = '1.7.3'
    implementation(libs.bundles.coroutines)


    //图像处理库Toolkit
    implementation(libs.renderscript.intrinsics.replacement.toolkit)

    //androidX
    implementation(libs.core.ktx)
    implementation(libs.appcompat.appcompat)
    implementation(libs.activity.ktx)
    implementation(libs.fragment.ktx)
    implementation(libs.preference.ktx)
    implementation(libs.androidx.documentfile)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.swiperefreshlayout)
    implementation(libs.androidx.recyclerview)
    implementation(libs.androidx.viewpager2)
    implementation(libs.androidx.webkit)

    //google
    implementation(libs.material)
    implementation(libs.flexbox)
    implementation(libs.gson)

    //lifecycle
    implementation(libs.lifecycle.common.java8)
    implementation(libs.lifecycle.service)

    //media
    implementation(libs.media.media)
    // For media playback using ExoPlayer
    implementation(libs.media3.exoplayer)
    // For loading data using the OkHttp network stack
    implementation(libs.media3.datasource.okhttp)
    // For exposing and controlling media sessions
    //implementation "androidx.media3:media3-session:$media3_version"

    //Splitties
    implementation(libs.splitties.appctx)
    implementation(libs.splitties.systemservices)
    implementation(libs.splitties.views)

    //room sql语句不高亮解决方法https://issuetracker.google.com/issues/234612964#comment6
    implementation(libs.room.runtime)
    implementation(libs.room.ktx)
    //kapt("androidx.room:room-compiler:$room_version")
    ksp(libs.room.compiler)
    androidTestImplementation(libs.room.testing)

    //liveEventBus
    implementation(libs.liveeventbus)

    //规则相关
    implementation(libs.jsoup)
    implementation(libs.json.path)
    implementation(libs.jsoupxpath)
    implementation(project(path: ':modules:book'))
    implementation(project(path: ':modules:rhino'))

    //JS rhino

    //网络
    implementation(libs.okhttp)
    implementation(fileTree(dir: 'cronetlib', include: ['cronet_api.jar', 'cronet_impl_common_java.jar', 'cronet_impl_platform_java.jar', 'cronet_shared_java.jar', '*.aar']))
    implementation(libs.protobuf.javalite)

    //Glide
    implementation(libs.glide.glide)
    implementation(libs.glide.okhttp)
    ksp(libs.glide.ksp)

    //Svg
    implementation(libs.androidsvg)
    //Glide svg plugin
    implementation(libs.glide.svg)

    //webServer
    implementation(libs.nanohttpd.nanohttpd)
    implementation(libs.nanohttpd.websocket)

    //二维码
    //noinspection GradleDependency
    implementation(libs.zxing.lite)

    //颜色选择
    implementation(libs.colorpicker)

    //压缩解压
    implementation libs.libarchive

    //apache
    implementation(libs.commons.text)

    //MarkDown
    implementation(libs.markwon.core)
    implementation(libs.markwon.image.glide)
    implementation(libs.markwon.ext.tables)
    implementation(libs.markwon.html)

    //转换繁体
    implementation(libs.quick.chinese.transfer.core)

    //加解密类库,有些书源使用
    //noinspection GradleDependency,GradlePackageUpdate
    implementation(libs.hutool.crypto)

    //firebase, 崩溃统计和性能统计
    // implementation platform(libs.firebase.bom)
    // implementation libs.firebase.analytics
    // implementation libs.firebase.perf
    // Firebase removed: analytics and perf are no longer included in the app

    implementation libs.glide.recyclerview

    //LeakCanary, 内存泄露检测
    //debugImplementation('com.squareup.leakcanary:leakcanary-android:2.7')

    //com.github.AmrDeveloper:CodeView代码编辑已集成到应用内
    //epubLib集成到应用内
}
