import java.security.MessageDigest

apply plugin: 'de.undercouch.download'

def BASE_PATH = "https://storage.googleapis.com/chromium-cronet/android/" + CronetVersion + "/Release/cronet/"
def assetsDir = projectDir.toString() + "/src/main/assets"
def libPath = projectDir.toString() + "/cronetlib"
def soPath = projectDir.toString() + "/src/main/jniLibs"

/**
 * 从文件生成MD5
 * @param file
 * @return
 */
static def generateMD5(final file) {
    MessageDigest digest = MessageDigest.getInstance("MD5")
    file.withInputStream() { is ->
        byte[] buffer = new byte[1024]
        //noinspection GroovyUnusedAssignment
        int numRead = 0
        while ((numRead = is.read(buffer)) > 0) {
            digest.update(buffer, 0, numRead)
        }
    }
    return String.format("%032x", new BigInteger(1, digest.digest())).toLowerCase()
}

/**
 * 下载Cronet相关的jar
 */
tasks.register('downloadJar', Download) {
    src([
            BASE_PATH + "cronet_api.jar",
            BASE_PATH + "cronet_impl_common_java.jar",
            BASE_PATH + "cronet_impl_native_java.jar",
            BASE_PATH + "cronet_impl_platform_java.jar",
            BASE_PATH + "cronet_shared_java.jar"
    ])
    dest libPath
    overwrite true
    onlyIfModified true
    onlyIf {
        def required = [
                'cronet_api.jar',
                'cronet_impl_common_java.jar',
                'cronet_impl_native_java.jar',
                'cronet_impl_platform_java.jar',
                'cronet_shared_java.jar'
        ]
        return required.any { !new File(libPath, it).exists() }
    }
}
/**
 * 下载Cronet的arm64-v8a so
 */
tasks.register('downloadARM64', Download) {
    src BASE_PATH + "libs/arm64-v8a/libcronet." + CronetVersion + ".so"
    dest soPath + "/arm64-v8a/libcronet." + CronetVersion + ".so"
    overwrite true
    onlyIf { !file(soPath + "/arm64-v8a/libcronet." + CronetVersion + ".so").exists() }
}
/**
 * 下载Cronet的armeabi-v7a so
 */
tasks.register('downloadARMv7', Download) {
    src BASE_PATH + "libs/armeabi-v7a/libcronet." + CronetVersion + ".so"
    dest soPath + "/armeabi-v7a/libcronet." + CronetVersion + ".so"
    overwrite true
    onlyIf { !file(soPath + "/armeabi-v7a/libcronet." + CronetVersion + ".so").exists() }
}
/**
 * 下载Cronet的x86_64 so
 */
tasks.register('downloadX86_64', Download) {
    src BASE_PATH + "libs/x86_64/libcronet." + CronetVersion + ".so"
    dest soPath + "/x86_64/libcronet." + CronetVersion + ".so"
    overwrite true
    onlyIf { !file(soPath + "/x86_64/libcronet." + CronetVersion + ".so").exists() }
}
/**
 * 下载Cronet的x86 so
 */
tasks.register('downloadX86', Download) {
    src BASE_PATH + "libs/x86/libcronet." + CronetVersion + ".so"
    dest soPath + "/x86/libcronet." + CronetVersion + ".so"
    overwrite true
    onlyIf { !file(soPath + "/x86/libcronet." + CronetVersion + ".so").exists() }
}

/**
 * 更新Cronet版本时执行这个task
 * 先更改gradle.properties 里面的版本号，然后再执行
 * gradlew app:downloadCronet
 */
tasks.register('downloadCronet') {
    dependsOn downloadJar, downloadARM64, downloadARMv7, downloadX86_64, downloadX86

    doLast {
        StringBuilder sb = new StringBuilder("{")
        def jniDir = new File(soPath)
        def files = [] as List
        if (jniDir.exists()) {
            jniDir.listFiles().each { dir ->
                if (dir.isDirectory()) {
                    def soFile = new File(dir, 'libcronet.' + CronetVersion + '.so')
                    if (soFile.exists()) files << [abi: dir.name, file: soFile]
                }
            }
        }
        for (entry : files) {
            println entry.abi
            sb.append("\"").append(entry.abi).append("\":\"").append(generateMD5(entry.file)).append("\",")
        }
        sb.append("\"version\":\"").append(CronetVersion).append("\"}")

        println sb.toString()

        println assetsDir
        def f1 = new File(assetsDir + "/cronet.json")
        if (!f1.exists()) {
            f1.parentFile.mkdirs()
            f1.createNewFile()
        }
        f1.text = sb.toString()

        // 将下载的 so 文件复制一份到 cronetlib（便于仓库检查）以及打包到 jniLibs 目录下以便在 APK 中加载
        // Copy found jniLibs so files into cronetlib (keep a repository copy) and ensure assets.json updated.
        files.each { entry ->
            def s = entry.file
            def destName = 'libcronet.' + CronetVersion + '.so'
            def destJarSo = new File(libPath, destName)
            try {
                s.withInputStream { ins -> destJarSo.withOutputStream { outs -> outs << ins } }
            } catch (e) {
                println "copy to cronetlib failed: ${e.message}"
            }
        }

    }
}
